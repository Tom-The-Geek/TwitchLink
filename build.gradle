plugins {
	id 'fabric-loom' version '0.5.43'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

def ENV = System.getenv()

minecraft {
}

repositories {
	maven {
		name = 'blay09-maven'
		url = 'https://artifactory.blay09.net/artifactory/libs-snapshot/'
	}
	maven {
		name = "TomTheGeek-mods"
		url = "https://dl.bintray.com/tomthegeek/mods/"
	}
}

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"

	implementation(include("net.blay09:javatmi:0.2.2-SNAPSHOT"))
	include("net.blay09:javairc:0.1.0-SNAPSHOT")

	modApi("me.sargunvohra.mcmods:autoconfig1u:3.3.1") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "me.sargunvohra.mcmods:autoconfig1u:3.3.1"

	modApi("me.shedaniel.cloth:config-2:4.8.3") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "me.shedaniel.cloth:config-2:4.8.3"

	modImplementation("io.github.prospector:modmenu:1.14.9+build.14") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	modApi("me.geek.tom:chat-icon-api:0.0.1") {
		exclude group: "net.fabricmc-fabric-api"
	}
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(jar) {
				builtBy remapJar
			}
			artifact("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar"){
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		if (ENV["BINTRAY_USER"]) {
			maven {
				url = 'https://api.bintray.com/maven/tomthegeek/mods/TwitchLink/'
				credentials {
					username = ENV["BINTRAY_USER"]
					password = ENV["BINTRAY_PASS"]
				}
			}
		}
	}
}
